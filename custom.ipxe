#!ipxe
###
### netboot.xyz-custom menu
###

### Fedora
set fedora_mirror http://mirror.lstn.net
set fedora_base_dir fedora

isset ${dhcp-server} && set ipparam ip=dhcp || set ipparam ip=${ip}::${gateway}:${netmask}:::none nameserver=${dns}
set ipparam BOOTIF=${netX/mac} ${ipparam}

goto start ||

:start
clear osversion
clear sku_type
clear ova
clear bt
set os Fedora
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch x86_64 ||
iseq ${os_arch} arm64 && set os_arch aarch64 ||

menu Custom Mirror ${os} - ${os_arch}
echo mirror ${fedora_mirror}
item --gap Latest Releases
item 42 ${space} ${os} 42
item 41 ${space} ${os} 41
item 40 ${space} ${os} 40
iseq ${os_arch} x86_64 && item rawhide ${space} ${os} rawhide ||
isset ${osversion} || choose osversion || goto custom_exit
set ova ${os} ${osversion}
goto product_sku

:product_sku
menu ${os} ${os_arch} sku type
item Everything ${ova} Everything
item Server ${ova} Server
item COSMIC-Atomic ${ova} COSMIC-Atomic
item Kinoite ${ova} Kinoite
item Silverblue ${ova} Silverblue 
item Sericea ${ova} Sericea
isset ${sku_type} || choose sku_type || goto fedora
set dir ${fedora_base_dir}/releases/${osversion}/${sku_type}/${os_arch}/os
iseq ${osversion} rawhide && set dir ${fedora_base_dir}/development/${osversion}/${sku_type}/${os_arch}/os ||
set ova ${ova} ${sku_type}
echo ${cls}
goto boottype


:boottype
echo kernel -> ${fedora_mirror}/${dir}/images/pxeboot/vmlinuz
menu ${os} ${os_arch} boot type
item graphical ${ova} graphical install
item text ${ova} text install
item rescue ${ova} rescue
item kickstart ${ova} set kickstart url [ ${ks_url} ]
isset ${bt} || choose bt || goto fedora
echo ${cls}
iseq ${bt} normal && goto boot ||
goto ${bt}

:graphical
set install_mode inst.graphical ||
goto bootos_images

:text
set install_mode inst.text ||
goto bootos_images

:rescue
set params inst.rescue ||
goto bootos_images

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ks_url
set params inst.ks=${ks_url} ||
clear bt
goto boottype

:bootos_images
imgfree
kernel ${fedora_mirror}/${dir}/images/pxeboot/vmlinuz inst.repo=${fedora_mirror}/${dir} ${install_mode} ${params} ${ipparam} initrd=initrd.magic ${cmdline}
initrd ${fedora_mirror}/${dir}/images/pxeboot/initrd.img
echo
echo MD5sums:
md5sum vmlinuz initrd.img
boot
goto linux_menu

goto custom_exit

:custom_exit
clear menu
exit 0
